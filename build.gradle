buildscript {
    repositories {
        gradlePluginPortal()
    }
    dependencies {
        classpath "io.quarkus:gradle-application-plugin:2.7.3.Final"
    }
}

ext {
    quarkusVersion = "2.7.3.Final"
    lombokVersion = "1.18.22"
    swaggerAnnotationsVersion = "1.6.5"

    liquibaseVersion = "4.8.0"
    picocliVersion = "4.6.3"
    jooqVersion = "3.16.4"
    postgresqlVersion = "42.3.3"
    jaxbVersion = "3.0.2"
}

allprojects {
    apply plugin: "java"

    group "ru.craftysoft"
    version "1.0.0"

    if (project.name != "db") {
        dependencies {
            implementation platform("io.quarkus:quarkus-bom:$quarkusVersion")
            implementation(
                    "io.quarkus:quarkus-resteasy-jackson",
                    "io.quarkus:quarkus-resteasy-mutiny",
                    "io.quarkus:quarkus-hibernate-validator",

                    "com.google.code.findbugs:jsr305",
                    "io.swagger:swagger-annotations:$swaggerAnnotationsVersion",
            )
        }
    }

    repositories {
        mavenCentral()
    }
}

apply plugin: "io.quarkus"

dependencies {
    implementation project(":db")
    implementation project(":registrar-api")
    implementation(
            "io.quarkus:quarkus-reactive-pg-client",
            "io.quarkus:quarkus-config-yaml",
            "io.quarkus:quarkus-smallrye-context-propagation",
    )
    compileOnly("org.projectlombok:lombok:$lombokVersion")
    annotationProcessor("org.projectlombok:lombok:$lombokVersion")
}

quarkusBuild {
    nativeArgs {
        additionalBuildArgs = "--allow-incomplete-classpath"
    }
}

task stage(dependsOn: ["build"])